#version 140

in highp vec4 position;
in mediump vec2 texcoord0;
in mediump vec3 normal;

out mediump vec2 var_texcoord0;
out mediump float var_light;
out mediump float var_fog_factor;

uniform vs_uniforms
{
    uniform mediump mat4 mtx_worldview;
    uniform mediump mat4 mtx_proj;
    uniform mediump mat4 mtx_world;
    uniform mediump vec4 light;
    uniform mediump vec4 fog_params;
};

void main()
{
    var_texcoord0 = texcoord0;
    
    // Transform normal to world space
    // mtx_world is necessary because the entity (mouse) rotates
    vec3 world_normal = normalize((mtx_world * vec4(normal, 0.0)).xyz);
    
    // Calculate diffuse lighting (directional light is also world-space)
    float diff = max(dot(world_normal, normalize(light.xyz)), 0.0);
    var_light = diff + light.w; 
    
    
    // Fog calculation
    vec4 p = mtx_worldview * position;
    float dist = length(p.xyz);
    var_fog_factor = clamp((fog_params.y - dist) / (fog_params.y - fog_params.x), 0.0, 1.0);

    gl_Position = mtx_proj * p;
}
